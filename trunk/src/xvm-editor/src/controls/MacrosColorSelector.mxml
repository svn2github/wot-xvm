<?xml version="1.0" encoding="utf-8"?>
<a:ColorSelector xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:a="controls.*"
    xmlns:validators="validators.*"
    implements="controls.IMacrosValueControl"
    creationComplete="onCreationComplete()">
    <fx:Script>
        <![CDATA[
            import mx.controls.Menu;
            import mx.events.MenuEvent;
            import mx.binding.utils.BindingUtils;
            import utils.Embed;

            include "include_MacrosValueControl.as";

            private function onCreationComplete():void
            {
                ti.restrict = '#0-9A-Za-z\{\}\-_:';
                ti.maxChars = 20;
                BindingUtils.bindProperty(tv, "allowedValues", this, [ "allowedValues" ]);
            }

            private var _value:String = "0";
            [Bindable]
            public override function get value():*
            {
                return isNaN(parseInt(_value)) ? _value : "0x" + toHex(parseInt(_value));
            }
            public override function set value(v:*):void
            {
                _value = v;
                if (v == null)
                {
                    ti.text = "(system)";
                    ti.alpha = 0.5;
                    cp.selectedColor = 1;
                    cp.alpha = 0.5;
                }
                else
                {
                    ti.text = isNaN(parseInt(v)) ? v : "#" + toHex(parseInt(v));
                    ti.alpha = 1;
                    cp.selectedColor = parseInt(v) || 1;
                    cp.alpha = isNaN(parseInt(v)) ? 0.5 : 1;
                }
            }

            protected override function textChanged():void
            {
                ti.alpha = 1;
                ti.validateNow();
            }

            protected override function onChange(event:Event):void
            {
                if (event)
                    event.stopPropagation();

                if (event.target == cp)
                {
                    ti.text = "#" + toHex(cp.selectedColor);
                    return;
                }

                var v:String = null;
                if (event.target == tv)
                    v = ti.text.charAt(0) == "#" ? "0x" + ti.text.substr(1) : ti.text;
                else if (event.target == pm)
                {
                    v = (event as MenuEvent).item.value;
                    (pm.popUp as Menu).selectedIndex = 0;
                } else
                    return;

                if (value != v)
                {
                    value = v;
                    dispatchEvent(new Event(Event.CHANGE));
                }
            }
        ]]>
    </fx:Script>
    <mx:Spacer width="5"/>
    <mx:PopUpMenuButton id="pm" width="40" dataProvider="{menuData}" label="" icon="{Embed.syscolors}" labelFunction="getLabel"
        toolTip="{resourceManager.getString('components', 'UseSystemColor')}" itemClick="onChange(event)"/>
</a:ColorSelector>
