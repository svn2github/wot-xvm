<?xml version="1.0" encoding="utf-8"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    borderVisible="true"
    minHeight="20"
    width="100%">
    <fx:Script>
    <![CDATA[
        import flash.events.Event;
        import events.SetDefaultValueEvent;
        import events.ValueChangedEvent;

        [Bindable(event="languageChanged")]
        public function _(s:String):String
        {
            return resourceManager.getString('panels', s);
        }

        [Bindable(event="languageChanged")]
        public function _m(s:String):String
        {
            return resourceManager.getString('markers', s);
        }

        public override function set mxmlContent(value:Array):void
        {
            if (super.numElements == 0)
            {
                // Me
                super.mxmlContent = value;
            }
            else
            {
                // Child
                var len:int = value.length;
                for (var i:int = 0; i < len; ++i)
                    holder.addElement(value[i]);
            }
        }

        [Bindable]
        public var config:String = null;

        protected function onChange(event: Event):void
        {
            if (event)
                event.stopPropagation();
            var target:DefaultComponent = event.target as DefaultComponent;
            if (target)
                dispatchEvent(new events.ValueChangedEvent(target));
        }

        protected function onSetDefault(event: Event):void
        {
            if (event)
                event.stopPropagation();
            dispatchEvent(new events.SetDefaultValueEvent(this));
        }
    ]]>
    </fx:Script>
    <fx:Metadata>
        [Event(name="valueChanged", type="events.ValueChangedEvent")]
        [Event(name="setDefaultValue", type="events.SetDefaultValueEvent")]
        [DefaultProperty("mxmlContent")]
        [ResourceBundle("panels")]
        [ResourceBundle("markers")]
    </fx:Metadata>
    <mx:Image right="5" y="-18" width="16" height="16" source="@Embed(source='images/revert.png')" toolTip="{_('DefaultSettings')}" useHandCursor="true" buttonMode="true" click="onSetDefault(event)"/>
    <s:VGroup id="holder" left="10" right="10" top="5" bottom="5" gap="2" variableRowHeight="true"/>
</s:Panel>
