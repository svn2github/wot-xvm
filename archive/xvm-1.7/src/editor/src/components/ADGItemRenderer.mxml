<?xml version="1.0" encoding="utf-8"?>
<s:MXAdvancedDataGridItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 dataChange="dataChanged(event)">
	<fx:Script>
		<![CDATA[
			import events.XVMValueEvent;

			import mx.controls.Image;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;

			import spark.components.CheckBox;
			import spark.components.Label;
			import spark.components.NumericStepper;
			import spark.components.TextInput;

			import utils.Const;
			import utils.ItemKind;
			import utils.UIUtils;

			[Embed(source="no.png")]
			[Bindable]
			public var imgNo:Class;			
			
			private function onChange(event:Event):void
			{
				var value:* = UIUtils.getComponentValue(event.target as UIComponent);
				dispatchEvent(new XVMValueEvent(Const.ADGItemRenrerer, value, data));
			}

			private function setDefaultClick(event:Event):void
			{
				dispatchEvent(new XVMValueEvent(Const.ADGItemRenrerer, null, data));
			}

			private function createItem(o:Object):UIComponent
			{
				if (!o || !o.hasOwnProperty("item"))
					throw new Error("ADGItemRenderer.createItem(): null argument");

				var result:UIComponent = utils.ItemKind.CreateObject(o["item"], onChange) as UIComponent;

				if (!result)
					throw new Error("ADGItemRenderer.createItem(): error creating object: " + o["item"]);

				if (o.hasOwnProperty("params"))
				{
					for (var v:String in o["params"])
					{
						if (result.hasOwnProperty(v))
							result[v] = o["params"][v];
						else
						{
							throw new Error("Cannot assign item property: " +
								o["item"] + "." + v + "=" + o["params"][v].toString());
						}
					}
				}

				if (o.hasOwnProperty("value"))
					UIUtils.setComponentValue(result, o["value"]);

				return result;
			}

			protected function dataChanged(event:FlexEvent):void
			{
				try
				{
					g.removeAllElements();
					if (data.hasOwnProperty("item"))
					{
						g.addElement(createItem(data) as IVisualElement);

						var i:Image = new Image();
						i.width = 10;
						i.height = 10;
						i.toolTip = "Установить значение по умолчанию";
						i.source = imgNo;
						i.addEventListener("click", setDefaultClick);
						g.addElement(i);
					}
				}
				catch (e:Error)
				{
					var l:Label = new Label();
					l.text = e.toString();
					l.toolTip = l.text;
					g.addElement(l);
				}
			}

		]]>
	</fx:Script>
	<s:HGroup id="g" horizontalAlign="center" verticalAlign="middle" left="4" right="4" gap="4"/>
</s:MXAdvancedDataGridItemRenderer>
