<?xml version="1.0" encoding="utf-8"?>
<a:ValueControl xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:a="controls.*"
    xmlns:validators="validators.*"
    gap="-1">
    <fx:Script>
        <![CDATA[
            protected static function toHex(v:int):String
            {
                return ("00000" + v.toString(16).toUpperCase()).substr(-6);
            }

            private var _value:int = 0;
            [Bindable]
            public override function get value():*
            {
                return "0x" + toHex(_value);
            }
            public override function set value(v:*):void
            {
                _value = int(v);
                ti.text = "#" + toHex(_value);
                cp.selectedColor = _value;
            }

            protected function textChanged():void
            {
                ti.validateNow();
            }

            protected override function onChange(event:Event):void
            {
                if (event)
                    event.stopPropagation();

                if (event.target == cp)
                {
                    ti.text = "#" + toHex(cp.selectedColor);
                    return;
                }

                var v:String = null;
                if (event.target == tv)
                    v = "0x" + ti.text.substr(1);
                else
                    return;

                if (value != v)
                {
                    value = v;
                    super.onChange(event);
                }
            }

            protected function onFocusIn():void
            {
                // virtual
            }

            protected function onFocusOut():void
            {
                // virtual
            }
        ]]>
    </fx:Script>
    <fx:Declarations>
        <validators:TextColorValidator id="tv" source="{ti}" property="text" valid="onChange(event)" invalid="{cp.selectedColor=1}"/>
    </fx:Declarations>
    <s:TextInput id="ti" width="100%" maxChars="7" restrict="#0-9A-Fa-f" change="textChanged()" focusIn="onFocusIn()" focusOut="onFocusOut()"/>
    <mx:ColorPicker id="cp" change="onChange(event)" useHandCursor="true" buttonMode="true"/>
</a:ValueControl>
